apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

def static releaseTime() {
    return new Date().format("yyyyMMdd_HHmmss")
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    flavorDimensions "default"

    defaultConfig {
        applicationId "com.process.factory"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode VERSION_CODE as int
        versionName VERSION_NAME
    }

    sourceSets {
        main {
            jniLibs.srcDir 'libs'
        }
    }

    productFlavors {
        baseProject {
        }
    }
    signingConfigs {
        release {
            storeFile file("../buildsystem/andr.jks")
            storePassword KEYSTORE_PASSWORD
            keyAlias KEYALISA
            keyPassword KEY_PASSWORD
        }
    }

    lintOptions {
        quiet true
        abortOnError false
        ignoreWarnings true
        disable 'InvalidPackage'            //Some libraries have issues with this.
        disable 'OldTargetApi'
        //Lint gives this warning but SDK 20 would be Android L Beta.
        disable 'IconDensities'             //For testing purpose. This is safe to remove.
        disable 'IconMissingDensityFolder'  //For testing purpose. This is safe to remove.
    }

    buildTypes {
        debug {
            buildConfigField("String", "API_HOST", "\"http://10.169.16.26:8080/\"")
            resValue "string", "app_name", "base-project_test"
            versionNameSuffix "-debug"
            applicationIdSuffix ".debug"
            minifyEnabled false
            debuggable true
        }

        preannounce {
            buildConfigField("String", "API_HOST", "\"http://192.168.49.139/\"")
            resValue "string", "app_name", "base-project_preannounce"
            versionNameSuffix "-preannounce"
            applicationIdSuffix ".preannounce"
            minifyEnabled false
            signingConfig signingConfigs.release
            debuggable true
        }

        release {
            buildConfigField("String", "API_HOST", "\"http://192.168.49.139/\"")
            resValue "string", "app_name", "base-project_release"
            versionNameSuffix "-release"
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }


    applicationVariants.all { variant ->
        variant.outputs.all {
            def fileName = "process-factory"

            if (variant.buildType.name == "release") {
                fileName += "_${defaultConfig.versionName}"
            } else if (variant.buildType.name == "preannounce") {
                fileName += "_${releaseTime()}_${defaultConfig.versionName}"
            } else if (variant.buildType.name == "debug") {
                fileName += "_${releaseTime()}"
            }
            fileName += "_${variant.buildType.name}.apk"
            outputFileName = fileName
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$rootProject.ext.kotlinLibraryVersion"
    implementation "com.android.support:design:$rootProject.ext.supportLibraryVersion"
    implementation "com.android.support:appcompat-v7:$rootProject.ext.supportLibraryVersion"
    implementation "com.android.support:support-v4:$rootProject.ext.supportLibraryVersion"
    implementation "com.github.bumptech.glide:glide:$rootProject.ext.glideLibraryVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$rootProject.ext.glideLibraryVersion"
    implementation "com.github.bumptech.glide:okhttp3-integration:$rootProject.ext.glideLibraryVersion"
    implementation "com.leinyo.android:httpclient:$rootProject.ext.httpClientLibrary"
    implementation "org.apache.commons:commons-lang3:$rootProject.ext.commonsLangVersion"
    implementation "com.amap.api:map2d:$rootProject.ext.ampMapLibraryVersion"
    implementation "com.leinyo.android:appbar:$rootProject.ext.appbarLibraryVersion"
    testImplementation 'junit:junit:4.12'
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'org.apache.commons:commons-collections4:4.0'
    implementation "com.android.support:multidex:$rootProject.ext.multidexLibraryVersion"
}
